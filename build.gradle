plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.2'
    id 'net.researchgate.release' version '2.3.4'
    id 'ru.vyarus.quality' version '1.2.0'
    id 'ru.vyarus.github-info' version '1.0.0'
    id "com.github.ben-manes.versions" version "0.11.3"
}

sourceCompatibility = 1.7

wrapper {
    gradleVersion = 2.9
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.test.runtimeClasspath.join("\n")
    }
}

repositories { jcenter() }
dependencies {
    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testRuntime files(createClasspathManifest)
}

group = 'ch.poochiethecat'
description = 'Plugin for Handling Symbolic Links using java.nio'

github {
    user 'poochiethecat'
    license 'MIT'
}



pluginBundle {
    description = 'Plugin for Handling Symbolic Links using java.nio'
    tags = ['symbolic links', 'java.nio']

    plugins {
        githubInfoPlugin {
            id = 'ch.poochiethecat.symlinks'
            displayName = project.description
        }
    }

    
}
// HOTFIX: plugin-publish don't see properties in global gradle.properties
System.setProperty('gradle.publish.key', project.properties['gradle.publish.key'] ?: '')
System.setProperty('gradle.publish.secret', project.properties['gradle.publish.secret'] ?: '')

afterReleaseBuild {
    dependsOn = [publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events "skipped", "failed", "standard_error"
        exceptionFormat "full"
    }
    maxHeapSize = "512m"
}

dependencyUpdates.revision = 'release'
